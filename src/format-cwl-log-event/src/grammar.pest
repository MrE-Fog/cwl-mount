// - $$ is an escape; it is replaced with a single $.
// - $identifier names a substitution placeholder matching a mapping key of "identifier".
//   By default, "identifier" is restricted to any case-insensitive ASCII alphanumeric string
//   (including underscores) that starts with an underscore or ASCII letter. The first
//   non-identifier character after the $ character terminates this placeholder specification.
// - ${identifier} is equivalent to $identifier. It is required when valid identifier
//   characters follow the placeholder but are not part of the placeholder, such as 
//   "${noun}ification".
// - Any other appearance of $ in the string will result in a ValueError being raised.
//
// See https://github.com/python/cpython/blob/main/Lib/string.py


format = _{
    SOI ~ element+ ~ EOI
}

element = _{
    escaped_delimiter |
    variable |
    literal
}

escaped_delimiter = @{delimiter ~ delimiter}

delimiter = _{"$"}

identifier = @{("_" | ASCII_ALPHA_LOWER) ~ (ASCII_ALPHANUMERIC | "_")*}

variable = ${
    delimiter ~ "{" ~ identifier ~ "}" |
    delimiter ~ identifier
}

literal = @{ char+ }
char = {
    !(delimiter) ~ ANY
}